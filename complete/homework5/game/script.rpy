# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.

define v = Character('Владос', color="#ffffff")
define r = Character('Владимир Жабовица', color="#ffffff")
define p = Character('Квакица', color="#ffffff")
define b = Character('Брусника', color="#ffffff")
define l = Character('Лягунид', color="#ffffff")
define a = Character('Кварсений', color="#ffffff")
define w = Character('Квакуля', color="#ffffff")
define i = Character('Яна (лягушка)', color="#ffffff")
define c = Character('Чинквак', color="#ffffff")
define s = Character('Кваконя', color="#ffffff")
define h = Character('Human Vlados', color="#ffffff")

# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.

# Игра начинается здесь:
label start:
    scene black
    v '. . .'
    v '. . .'
    v 'ха...'
    v 'ха... ха-ха... ха...'
    scene bg room with fade
    "Ты открываешь глаза. Ты не помнишь, что тебе снилось, но губы почему-то до сих пор растянуты в блаженной улыбке."
    v "А?..."
    "Ты утираешь слюну с подбородка."
    v "Уф... Какой сегодня... день... сколько времени?... Восемь тридцать!? Да почему я никак не могу проснуться вовремя!?"
    "Ты спешно одеваешься и бежишь в школу. Все утренние приготовления - натянуть школьную форму, собрать рюкзак, опрокинуть в себя чашку кофе - ты делаешь уже настолько машинально, что это заставляет задуматься."
    jump school

label school:
    scene bg classroom full with fade
    "Еще не зайдя в класс, ты прокручиваешь в голове вопросы, которые ты задашь своим друзьям. А что ещё сказал директор? А Содалв снова побрёл домой, когда очнулся?..."
    "Но..."
    v "?..."
    "В классе по-прежнему шумно и душно, по-прежнему жабки занимаются своими делами. Однако целый ряд почему-то пустует. Ряд... на котором обычно сидят твои друзья."
    show brusneeka
    "Со звонком в класс заходит та загадочная резкая учительница. Не желая оказаться у нее на пути, ты спешно садишься за одну из парт на пустом ряду. Тебе становится немного не по себе."
    "Кто-то из класса" "Б-брусника Арсенидовна..."
    "Ты погружаешься в свои мысли и напрочь забываешь об уроке и об учительнице. Где же твои друзья? Неужели директор что-то с ними сделал? Или не директор - а Содалв? Или кто-то еще..."
    b "...Содалв?"
    v "А!?"
    b "...ответь."
    hide brusneeka

    default menuset = set()
    
    menu lesson:

        set menuset
        "Настоящая фамилия этой персонаж_ки - Соло."

        "Женщина-кошка":
            show brusneeka
            b "..." 
            b "Нет."
            jump school2

        "Кайло Рен":
            show brusneeka
            b "...верно. {i}Не забудь. Запиши.{/i} Важно."
            jump school2

        "Арина":
            show brusneeka
            b "..." 
            b "Нет."
            jump school2

label school2:
    "Ты слышишь, как звенит звонок. Ты понимаешь, что больше не высидишь - делать что-то нужно прямо сейчас. Ты вновь стремительно покидаешь кабинет - даже не заметив, что Брусника и не пытается тебя остановить."
    jump afterschool

label afterschool:
    scene bg hallway with fade
    "За перемену ты успеваешь выяснить, что директор сегодня тоже отсутствует. Когда начинается урок и все разбредаются по кабинетам, ты остаёшься будто бы совсем один..."
    v "Надо подышать... успокоиться..."
    "Ты дышишь и успокаиваешься. Ты быстро находишь спрятанный ребятами ключ от компьютерного класса - его Кварсений спрятал в цветочный горшок в коридоре."
    "Остаётся только попасть в кабинет информатики. За дверью никого не слышно, значит, урока сейчас нет. Можно заходить."
    "*Клик! Клак!*"
    scene bg computer room with fade
    "Ты открываешь дверь и осторожно, даже с опаской заглядываешь внутрь. В комнате темно, жарко и пусто. Кого ты ожидал там увидеть, Содалва? Ты сам сейчас Содалв - а значит, бояться нечего."
    v "Директор сказал, у Содалва тут свой компьютер..."
    "Ты садишься за компьютер в самом дальнем углу класса и включаешь его. Машина начинает равномерно жужжать, по темноте кабинета разливается голубоватый свет - и вот тебя уже встречает стартовый экран Windows XP."
    v "Ага... пользователь 'Ученик' и пользователь 'Админ'... Второй, конечно, запаролен. Как там было?"
    jump computer

label computer:

    $ password = renpy.input("Введите пароль (до 8 символов)", length=8)

    if password == "Persona5":
        "Windows встречает тебя знакомым звуком. Получилось! На экране рабочего стола ты видишь анимешную девочку. Какое-то время ты тратишь на то, чтобы понять, как она так изогнулась."
        jump explore
    else:
        "Ошибка! Неверный пароль. Проверьте написание и регистр."
        v "Чёрт! Как там было..."
        jump computer

label explore:
    v "Так, ну чё тут у нас..."
    "Ты открываешь последние запущенные программы. Пинболл, MS Paint, Скайп и... нечто под названием..."
    show vlados frightened
    v "HACK-MY-WORLD...EXE?..."
    hide vlados frightened
    "Какое-то время ты колеблешься, но в итоге запускаешь программу. Она разворачивается в небольшое окошко по центру экрана - и твоему взгляду предстает перегруженный интерфейс с кучей кнопок и вкладок."
    "Среди разделов с непонятными названиями ты находишь вкладку OpenWorlds. На экране высвечивается список почтовых адресов."
    v "Так, секунду... первый же адрес... sodalv666@gmail.com!? И дата... 9 октября..."
    v "А потом - BIBA@gmail.com, rucawetsa@gmail.com... iana.senpai@gmail.com, ciao.sonya@gmail.com, pteetsa2000@gmail.com, queen.weeka@gmail.com, arrrseny@gmail.com, chingiz.han@gmail.com..."
    v "И ещё какой-то furry.overlord@rambler.ru..."
    v "И дата... 12 октября! Вчера!"
    "Ты проводишь какое-то время, тыкаясь в разные разделы."
    "Постепенно картина начинает проясняться - эта небольшая программа помогает подбирать пароли к аккаунтам. Судя по всему, аккаунты из вкладки OpenWorlds уже взломаны."
    v "А ведь... у меня же телефон Содалва... Что если..."
    "Ты берёшь в руки телефон и открываешь почтовое приложение. Оно начинает подгружаться, но потом сообщает - неверный пароль. Неужели кто-то и правда взломал его аккаунт?"
    v "Но вдруг там есть что-то важное... Точно! Что если я запрошу пароль?..."
    "Оказывается, Содалв был неглуп и озаботился двухфакторной аутентификацией. Поэтому ты без проблем запрашиваешь восстановление пароля по номеру телефона."
    "Остаётся только дождаться СМС. Ты чувствуешь, как от волнения у тебя начинает побаливать живот. Ты нутром чуешь, что разгадка уже совсем близко..."
    "Как вдруг..."
    play sound "audio/skype.mp3" loop
    "Ты слышишь звонок."
    "На мониторе появляется типичная скайповская плашка входящего вызова. Ты читаешь имя пользователя, и чувствуешь, как внутри что-то обрывается."
    v "Владос... вызывает..."
    v ". . ."
    "Мир кто-то словно ставит на паузу. Теперь в нём есть только ты, тёмный, душный кабинет информатики, свет монитора на твоем лице и бесконечный звук звонка из лучшего файлообменника..."

    menu answer:
        "Ответить на звонок":
            stop sound
            scene bg skype with fade
            v "!!!!!!!????????"
            "Этого не может быть. Тебе это снится. Да, точно. Другого объяснения быть не может."
            "С экрана на тебя смотрит..."
            show vlados real with fade
            h ". . ."
            "Владос. Человеческий Владос. То есть, ты сам. То есть, твое тело... Однако ты замечаешь в его глазах что-то, что делает всё лицо будто... чужим."
            h ". . ."
            h "Меня слышно?"
            v "...слышно. А меня?"
            h "Слышно."
            v ". . ."
            h ". . ."
            h "Что ж, ты оказался довольно умён. Не ожидал, что ты зайдешь так далеко."
            v "Что всё это значит!? Почему ты в моём теле!?"
            h "(ухмыляется) Что, здесь я должен толкнуть злодейскую речь? Хорошо..."
            h "В целом, ты всё правильно понял. Мы с тобой поменялись местами. Точнее, я вселился в тебя..."
            v "Но как ты это сделал?"
            h "А что, ты уже забыл это письмо от 9 октября? С программой внутри? Забыл, как запустил её?"
            v "Я всё помню. Но как это работает?"
            h "..."
            h "Я... не до конца уверен."
            v "Ну опиши по шагам."
            h "(цыкает) Вот привязался... Ладно. В свободное время я писал программу, которая взламывает аккаунты. Ну, в 'Моём мире'. Есть у вас такая соцсеть?"
            v "...когда-то была."
            h "Ну вот, а у нас есть сейчас. Я закончил прототип программы и решил потестировать ее на настоящем аккаунте. Своём. В общем, свой аккаунт я успешно взломал. Но... Не только аккаунт."
            h "Кажется, в прототипе программы был баг? И я взломал не только аккаунт, но... связь между мирами? Взломал... мой мир?"
            v "Чегоооо."
            h "(усмехается) Ну а дальше ты понимаешь. Программа до сих пор работает с перебоями, поэтому нас с тобой кидает туда-сюда. Но я уже почти дописал её!"
            h "Так, например, я научился удалять пользователей. Как по-твоему, где сейчас твои жалкие друзья?"
            v "..."
            h "Да-да! И твои реальные друзья, и эти тупые лягушки - никто мне не помешает! И этот идиотский директор, который не смог выполнить самую простую просьбу..."
            v "Это он furry.overlord@rambler.ru?"
            h "Точно так. Ненавижу фурри."
            v "Да как ты смеешь!"
            h "Мне остался последний шаг. Удалить тебя - и всё закончится. А у меня останется это прекрасное тело... и я смогу быть в нём сколько захочу..."
            "Человеческий Владос на экране берет в руки эспандер. Ты слышишь, как эспандер начинает трещать."
            h "И сотру..."
            h "всех..."
            h "в порошок..."
            "Кажется, человеческий Владос стремился для драматического эффекта сломать эспандер, но у него это не вышло. Он сильно тужится и видимо забывает о твоем присутствии."
            hide vlados real
            v "(про себя) Быстро. Что я могу сделать?"
            v "(про себя) Эта программа, про которую он говорит... Которую я скачал... Кажется, там были исходники. Что, если я могу всё исправить? Залезть прямо в скрипт..."
            v "(про себя) Чтобы спасти моих друзей... я должен... переписать его код!!!"
            "Ты прикидываешь план дальнейших действий. Cкачать Renpy. Открыть в архиве, который ты сегодня скачал, папку homework6. Найти там script.rpy. А дальше... дальше действовать по ситуации."
            "Ты слышишь, как хрустит эспандер - или руки человеческого Владоса."
            "Нет времени объяснять. Выключай игру. Действуй."
            return

