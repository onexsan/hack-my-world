# Вы можете расположить сценарий своей игры в этом файле.

# Определение персонажей игры.

define v = Character('Владос', color="#ffffff")
define r = Character('Владимир Жабовица', color="#ffffff")
define p = Character('Квакица', color="#ffffff")
define b = Character('Баржа', color="#ffffff")
define l = Character('Лягунид', color="#ffffff")
define a = Character('Кварсений', color="#ffffff")
define w = Character('Квакуля', color="#ffffff")
define i = Character('Яна (лягушка)', color="#ffffff")
define c = Character('Чинквак', color="#ffffff")
define s = Character('Кваконя', color="#ffffff")
define d = Character('Директор', color="#ffffff")

# Вместо использования оператора image можете просто
# складывать все ваши файлы изображений в папку images.
# Например, сцену bg room можно вызвать файлом "bg room.png",
# а eileen happy — "eileen happy.webp", и тогда они появятся в игре.

# Игра начинается здесь:
label start:
    scene black
    "Ты просыпаешься от головной боли. В комнате очень душно. Ты невольно вспоминаешь Озона. Ты задумываешься, существует ли где-нибудь в этом мире Озон. Квакозон?..."
    scene bg room with fade
    v "Уф..."
    "Вдоволь покряхтев, ты наконец поднимаешься. Как и в прошлый раз, вокруг царит бардак."
    v "И что он тут делает целыми днями, тут же даже компьютера нет..."
    "С рабочего стола на тебя многозначительно смотрит томик Фрейда. Закладка в нем осталась на прежнем месте."
    "Кажется, ты и в этот раз не находишь ничего интересного - остаётся только отправиться в школу."
    
    jump school

label school:
    scene bg classroom full with fade
    "Ты никак не научишься выходить заранее и поэтому снова вбегаешь в класс за пару минут до звонка. Твои новые друзья все так же встречают тебя опасливыми взглядами."
    v "Ребята, это я…"
    show leoneed at left
    l "Ура, друг!"
    show arseny at right
    a "Привет, друг!"
    hide leoneed
    hide arseny
    "Вы обмениваетесь новостями. Содалв будто бы испарился - по словам ребят, он по-прежнему не ходит в школу. Жабовица пробовал караулить его у дома после занятий, но обычно не выдерживал и шел кушать."
    v "Энивэй, с этим надо что-то делать."
    show weeka at left
    w "Да, только мы никак не можем выследить Содалва..."
    show iana at right
    i "Может, нам стоит пообщаться с директором? Он явно в этом как-то замешан..."
    hide weeka
    hide iana
    "Сразу после этих слов раздается звонок. В класс заходит та круглая учительница с маленьким лицом и невероятно красивыми глазами."
    show barzha
    "Кто-то из учеников" "Баржа Ильинишна!..."
    v "(про себя) Это Баржа!?"
    "Занятия в этой школе не перестают тебя удивлять. Тема сегодняшнего урока - аниме. Баржа то и дело ловит твой заинтересованный взгляд."
    b "(ласково улыбаясь) Содалв, расскажи нам..."
    hide barzha

    default menuset = set()
    
    menu lesson:

        set menuset
        "У какого аниме-персонажа есть искусственная рука?"

        "Спайк Шпигель":
            show barzha
            b "(с удивлением) К сожалению, это не так... Ладно, идем дальше!" 
            jump school2

        "Саске Учиха":
            show barzha
            b "(с удивлением) К сожалению, это не так... Ладно, идем дальше!"
            jump school2

        "Гатс":
            show barzha
            b "Конечно! Молодец, Содалв!"
            b "Советую всем {i}запомнить имя{/i} этого героя..."
            jump school2

label school2:
    hide barzha
    "В конце урока Баржа включает вам какой-то мультфильм. Какое-то время ты смотришь, как завороженный, но вспоминаешь, что у тебя есть дела поважнее. Ты поворачиваешься к сидящим сзади друзьям."
    v "Я согласен, раз мы не можем выследить Содалва - нам стоит хотя бы поговорить с директором."
    show pteetsa at right
    p "Всё ему предъявим, и твои оценки, и как тебя после уроков ни за что оставляли..."
    show rucaweetsa at left
    r "О, а ты не думал ещё поболтать с нашими знакомыми Кваконей и Чинкваком? Они у нас студенты по обмену! Если они пригрозят директору отчислением, его инфаркт хватит!"
    v "Какой странный план... но, наверное, я не откажусь от любой помощи..."
    hide rucaweetsa
    hide pteetsa
    "Ребята рассказывают тебе, что после уроков Чинквак и Кваконя помогают другим ученикам с английским."
    "Как только раздается звонок, ты срываешься с места и выбегаешь из класса - чтобы в этот раз тебя точно не смогли оставить заниматься какой-нибудь ерундой."
    show barzha
    b "Содалв!..."
    b "(со вздохом) Неужели ему так не понравился Токийский гуль..."
    hide barzha
    jump afterschool

label afterschool:
    scene bg classroom empty with fade
    "Ты без труда находишь кабинет, где Чинквак и Кваконя помогают другим ребятам с английским. К твоему удивлению, желающих учиться сегодня совсем нет - поэтому жабки занимаются своими делами."

    $ chingiz_complete = False
    $ sonya_complete = False
    
    menu aftertalk:

        "Поговорить с жабкой в очках":
            scene bg classroom empty with fade
            show sonia
            "Недолго думая, ты подсаживаешься к Кваконе. Та мгновенно прячет под парту небольшую записную книжку."
            s "Ciao! Hi there!"
            "Ты не ожидал, что Кваконя заговорит с тобой сразу на двух языках, но решаешь не терять лица."
            v "H-hi..."
            s "I didn't expect you to come but it's nice to see you!"
            v "Yeah, neither did I... I mean, nice to see you too!"
            s "I didn't know you needed help with English... is there anything I can do for you?"
            v "(глубоко вздыхает) Can we... can we talk about something entirely hypothetical?"
            s "(смеется) Go on."
            v "Let's say... There’s another world out there. And... You and some of our classmates exist there too. But I’m trapped here and I need your help. What would you say?"
            "Ты вкратце рассказываешь Кваконе о своем мире."
            s "Well, I'd ask you to tell me more about that other version of myself first!"
            hide sonia

            menu sonya_question:
                set menuset
                "Let me think... Which author have we discussed at our poetry club meetings?"

                "Марина Цветаева":
                    show sonia
                    "Ты рассказываешь Кваконе о той Соне, которую ты знаешь. Кваконя интересуется, в каких журналах публиковали Сонины стихи."
                    jump sonya2

                "Сильвия Плат":
                    show sonia
                    "Ты рассказываешь Кваконе о той Соне, которую ты знаешь. Кваконя интересуется, в каких журналах публиковали Сонины стихи."
                    jump sonya2

                "Рупи Каур":
                    show sonia
                    "Ты рассказываешь Кваконе о той Соне, которую ты знаешь. Кваконя интересуется, в каких журналах публиковали Сонины стихи."
                    jump sonya2

            label sonya2:
                s "Oh wow! Sounds amazing, I’d love to go there…"
                s "So you’re being serious… Okay… If you need my assistance, I’ll be happy to help!"
                v "Really? Thank you so much!"
                "Ты просишь Кваконю встретить тебя у кабинета директора. Разговаривать на английском тебе понравилось, и ты чувствуешь себя гораздо увереннее обычного."
                
                $ sonya_complete = True
                hide sonia

                if chingiz_complete is True and sonya_complete is True:
                    jump end
                else:
                    jump aftertalk
        
        "Поговорить со светловолосой жабкой":
            scene bg classroom empty with fade
            show chingiz
            "Ты подходишь к Чинкваку. Он отрывается от книги и смотрит на тебя спокойно и внимательно."
            c "Hallo."
            "Это hallo не похоже на обычное английское hello. Ты напрягаешься."
            c "Sprichst du kein Deutsch? Прабачце! Чым магу дапамагчы?"
            v ". . ."
            c "Maybe English? You’d better ask Kvakonya though, I’m not used to teaching it."
            v "I… just wanted to talk to you…"
            c "Так говори по-русски, свои же!"
            v "Уф… слушай, мне нужна помощь. Нужно шантажировать директора."
            c "Я заинтересован."
            "Ты вкратце рассказываешь Чинкваку о своих проблемах и о своем мире."
            c "О! Ого... а расскажи что-нибудь про меня…"
            hide chingiz

            menu chingiz_question:
                set menuset
                "Скажем… Какое божество мне ближе всего?"

                "Кали":
                    show chingiz
                    "Ты описываешь Чинкваку Чингиза. Тот уточняет у тебя Чингизов рецепт драников."
                    jump chingiz2

                "Ганеша":
                    show chingiz
                    "Ты описываешь Чинкваку Чингиза. Тот уточняет у тебя Чингизов рецепт драников."
                    jump chingiz2

                "Аматэрасу":
                    show chingiz
                    "Ты описываешь Чинкваку Чингиза. Тот уточняет у тебя Чингизов рецепт драников."
                    jump chingiz2

            label chingiz2:
                c "Хех! Интересно там у вас…"
                v "Так ты с нами?"
                c "Спрашиваешь! Как я могу остаться в стороне в такое время."
                "Ты просишь Чинквака встретить тебя позже у кабинета директора. Тот обещает подготовить пламенную речь."

                $ chingiz_complete = True
                hide chingiz

                if chingiz_complete is True and sonya_complete is True:
                    jump end
                else:
                    jump aftertalk

        
    
label end:
    scene bg hallway with fade
    "Чуть позже ты встречаешь весь свой отряд у кабинета директора."
    v "Ну что… все готовы?"

    menu knock:
        "Постучать в дверь":
            "Несмотря на то, что тебе уже почти 25, от мысли о ругани с директором тебе все равно становится не по себе. Особенно учитывая тот факт, что ты сейчас находишься в теле лягушонка-старшеклассника."
            "Именно поэтому ты на секунду замираешь, прежде чем постучать."
            "*Тук! Тук!*"
            "Голос" "…Войдите."
            "Вы с друзьями переглядываетесь и все вместе входите в кабинет."
            jump talk
    
label talk:
    scene bg principal room with fade
    "В кабинете директора почему-то очень тускло. Это крошечная комнатка - стулья для посетителей, стол и большое офисное кресло."
    "В кресле сидит грузная жаба в пиджаке и очках. По лицам друзей ты понимаешь - это и правда директор школы."
    show principal
    d "Содалв… с чем пришли на этот раз?"
    v "Мы пришли выяснить… нет, мы пришли доказать, что ваши обвинения в мою сторону беспочвенны!"
    d "Вот как… Слушаю."
    "Ты делаешь глубокий вдох. Всё нормально. У вас есть план."
    hide principal

    $ weeka_iana = False
    $ pteetsa_rucaweetsa = False
    $ chingiz_sonya = False
    $ arseny_leonid = False

    menu plan:
        "Призвать Кварсения и Лягунида":
            show bg principal room with fade
            show arseny at right
            a "(показывая фото с айфона) Так, простите, это не то, это кошка…"
            a "Вот сюда посмотрите! Видите, вот разбитое стекло, а вот кто-то покидает место преступления... И что это, если не ваша шляпа?"
            show leoneed at left
            l "Та самая, которая прямо сейчас висит у вас на крючке!"
            a "Господин директор, мы свидетели. Содалв не разбивал окно в учительской."
            l "(злобно) Это были вы! Вы подставили нашего друга!"
            hide leoneed
            hide arseny
            show principal
            d ". . ."
            hide principal

            $ arseny_leonid = True

            if weeka_iana is True and pteetsa_rucaweetsa is True and chingiz_sonya is True and arseny_leonid is True:
                jump finish
            else:
                jump plan

        "Призвать Квакулю и Яну (лягушку)":
            show bg principal room with fade
            show weeka at left
            w "(откладывая папку) …такой вывод можно сделать на основании страниц 4-5 Приложения 1 с копиями классного журнала за сентябрь 20** года."
            show iana at right
            i "Содалв не получал всех этих плохих оценок - вы их подделали."
            w "А это прямое нарушение школьного устава!"
            i "Я думаю, вы не хотите иметь дело с его родителями... со школьным советом... или, того хуже - с общественным мнением."
            w "(у нее две тысячи подписчиков)"
            hide weeka
            hide iana
            show principal
            d ". . ."
            hide principal

            $ weeka_iana = True

            if weeka_iana is True and pteetsa_rucaweetsa is True and chingiz_sonya is True and arseny_leonid is True:
                jump finish
            else:
                jump plan


        "Призвать Кваконю и Чинквака":
            show bg principal room with fade
            show chingiz at left
            c "(кладет листок бумаги на стол) Знаете, что это? Это письмо из первого в городе лицея. Говорят, меня - и Кваконю - там очень хотели бы видеть."
            show sonia at right
            s "(небрежно) Говорят, третья гимназия в последнее время на областных олимпиадах плетётся в самом конце..."
            c "Уходить, конечно, не хотелось бы..."
            s "Как думаете, с этим можно что-то сделать?"
            hide chingiz
            hide sonia
            show principal
            d ". . ."
            hide principal

            $ chingiz_sonya = True

            if weeka_iana is True and pteetsa_rucaweetsa is True and chingiz_sonya is True and arseny_leonid is True:
                jump finish
            else:
                jump plan

        "Призвать Жабовицу и Квакицу":
            show bg principal room with fade
            show pteetsa at right
            p "(Жабовица, ты знаешь, что добавить?)"
            show rucaweetsa at left
            r "(Если честно, я вообще не знаю, что мы с тобой тут делаем.)"
            p "(Как что? Содалва поддерживаем...)"
            r "ВОТ ЭТО ВЫ КОНЕЧНО ДАЁТЕ!"
            p "НУ-НУ! А ЕЩЕ ДИРЕКТОР!"
            hide pteetsa
            hide rucaweetsa
            show principal
            d ". . ."
            hide principal

            $ pteetsa_rucaweetsa = True

            if weeka_iana is True and pteetsa_rucaweetsa is True and chingiz_sonya is True and arseny_leonid is True:
                jump finish
            else:
                jump plan

label finish:
    show bg principal room with fade
    show principal
    d ". . ."
    d ". . ."
    "Ты замечаешь, что директор долго смотрит тебе прямо в глаза. Ты плотно сжимаешь губы и не отводишь от него взгляда."
    d "Может быть, я скажу сейчас какую-то чушь... Но..."
    d "...Я не знаю, кто ты. Знаю только, что ты не Содалв. Я вижу это по твоим глазам."
    v "!!!"
    d "Позвольте мне рассказать вам кое-что... Вы вроде неплохие ребята, раз так защищаете своего друга... Я надеюсь, что вы поймете меня."
    d "...примерно месяц назад Содалв начал странно себя вести."
    "Ты замечаешь, что директор перестает смотреть на тебя и теперь смотрит куда-то в сторону - будто говорит не о тебе."
    d "Как вы, может быть, знаете, он ходит в наш компьютерный кружок. Он очень способный мальчик, учителя его всегда хвалили..."
    d "Содалв часто оставался после уроков в компьютерном классе. Учителя доверяли ему и просили его закрыть за собой кабинет, когда он закончит. Но я запретил ему так делать. А он..."
    d "Он... я не знаю, как... он... нашёл... мои фото... мои переписки... и стал угрожать мне."
    show leoneed at left
    l "Ничего себе!"
    hide leoneed
    d "Он потребовал у меня ключ от компьютерного кабинета - и возможность посещать его в любое время. Я... я не смог отказать. Он иногда просиживал там целые вечера."
    d "Но недавно... Его требования стали совсем странными."
    d "Сначала он попросил меня подстроить сцену с разбитым стеклом... и обвинить в ней его самого!... Потом попросил испортить ему успеваемость... Зачем-то просил оставлять его после уроков..."
    "Ты замечаешь, что директор снова смотрит на тебя."
    d "Я просто делал, как он говорит. Оставлял его... тебя... после уроков."
    d "Содалв... я не знаю, что с ним... Не знаю, что у него в голове... Но ему, кажется, нужна помощь. Черт побери, я говорю с ним прямо сейчас... может быть, это мне нужна помощь..."
    "Тебя поражает и то, как директор повел себя в этой ситуации, и то, каким маленьким он сейчас кажется."
    v ". . ."
    v "Я обещаю, мы скоро со всем разберёмся. Вы всё ещё выходите с Содалвом на связь?"
    d "Нет. Но я знаю, что каждый вечер он проводит в компьютерном классе. Он приходит совсем поздно, когда в школе уже никого нет. Я..."
    d "...я боюсь привлекать к себе его внимание. И я буду рад, если вы сможете мне помочь. Вот вам пароль от его компьютера и запасной ключ."
    show pteetsa at right
    p "Спасибо, мужчина."
    hide pteetsa
    hide principal
    "Ключ вы с ребятами договариваетесь спрятать - у себя тебе его держать опасно, может найти Содалв. А пароль запомнить оказывается несложно - {i}Persona5{/i}."
    "Ты чувствуешь, как в кармане вибрирует телефон. У вас новая заметка!"
    "Назови же // отцу // имя дочери // там, где он // сетует на толпу."
    "{i}note from a friend: you can do it either offline or online.{/i}"
    v "О нет... Я, кажется, сейчас опять отключусь..."
    show sonia at right
    s "Не беспокойся! Мы постараемся все объяснить директору..."
    show chingiz at left
    c "И выследить Содалва!..."
    hide sonia
    hide chingiz
    show weeka at left
    w "He sleeps..."
    show arseny at right
    a "Real shit."
    hide weeka
    hide arseny

    scene black
    "Звуки смешиваются, и ты перестаешь хоть что-то понимать. Глаза закрываются сами собой."
    "Это так приятно - закрывать глаза. Тебе кажется, что так должно быть всегда."
    "Ты чувствуешь, что следующий сон точно будет последним."
    return
